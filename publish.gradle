def githubUser = "gildor"
def githubId = "$githubUser/nonnull-observables"
def projectTitle = "NonNullObservables"
def repoWeb = "https://github.com/${githubId}"
def repoVcs = "${repoWeb}.git"
def issueTracker = "${repoWeb}/issues"
def tags = ['android', 'databinding', 'ObservableField', 'NonNull', 'null safety']
def licenseId = "Apache-2.0"
def licenseName = "The Apache Software License, Version 2.0"
def licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
def releaseTag = "v${project.version}"

// Create the pom configuration:
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            name projectTitle
            description project.description
            url repoWeb
            developers {
                developer {
                    name "Andrey Mischenko"
                    email "git@gildor.ru"
                    organizationUrl "https://github.com/$githubUser"
                }
            }
            scm {
                url repoWeb
                connection "scm:git:${repoVcs}"
                developerConnection "scm:git:${repoVcs}"
                tag releaseTag
            }
            issueManagement {
                system "GitHub Issues"
                url issueTracker
            }
            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
        }
    }
}


bintray {
    user = project.properties['bintray.user']
    key = project.properties['bintray.key']
    configurations = ['archives']
    publish = true
    pkg {
        repo = project.properties['bintray.repo'] ?: 'maven'
        name = projectTitle
        desc = project.description
        githubRepo = githubId
        githubReleaseNotesFile = 'CHANGELOG.md'
        websiteUrl = repoWeb
        issueTrackerUrl = "${repoWeb}/issues"
        vcsUrl = repoVcs
        licenses = [licenseId]
        labels = tags
        version {
            name = project.version
            vcsTag = releaseTag
            mavenCentralSync {
                sync = project.properties['sonatype.user'] != null
                user = project.properties['sonatype.user']
                password = project.properties['sonatype.password']
                close = true
            }
        }
    }
}
